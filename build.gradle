plugins {
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'

    id 'nu.studer.jooq' version '6.0.1'
}

group = 'com.kwj'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    commons_lang3_version = "3.12.0"
    commons_collections4_version = "4.4"
}

dependencies {
    // official spring boot starter dependencies
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // general development library dependencies
    implementation "org.apache.commons:commons-lang3:$commons_lang3_version"
    implementation "org.apache.commons:commons-collections4:$commons_collections4_version"

    runtimeOnly 'com.h2database:h2'
    jooqGenerator 'com.h2database:h2'

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test'
                    user = 'test'
                    password = 'testpw'
                }
                generator {
                    strategy {
                        name = 'org.jooq.codegen.example.JPrefixGeneratorStrategy'
                    }
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        includes = 'public.*'
                        excludes = ''
                    }
                    target {
                        packageName = 'com.kwj.jooqt.domain.generated'
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}
